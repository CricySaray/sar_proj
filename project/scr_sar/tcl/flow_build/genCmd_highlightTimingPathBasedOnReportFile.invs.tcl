#!/bin/tclsh
# --------------------------
# author    : sar song
# date      : 2025/09/24 19:00:22 Wednesday
# label     : gui_proc
#   tcl  -> (atomic_proc|display_proc|gui_proc|task_proc|dump_proc|check_proc|math_proc|package_proc|test_proc|datatype_proc|db_proc|flow_proc|report_proc|cross_lang_proc|misc_proc)
#   perl -> (format_sub|getInfo_sub|perl_task)
# descrip   : Obtain the paths that need to be highlighted based on the report_timing RPT file generated by PT. and add some text and markers
# return    : cmds list
# ref       : link url
# --------------------------
alias degui "delete_all_gui_object_and_highlight"
proc delete_all_gui_object_and_highlight {} {
  delete_gui_object -all
  dehighlight -all
}
proc genCmd_highlightTimingPathBasedOnReportFile {args} {
  # for genCmd_getPurePinOfPath_fromTimingPathReport
  set reportTimingFile                    ""
  set ifAddMarkersAndText                 1 ; # 1|0
  set ifAddCellTypeOnTopOfInstRect        1
  set ifAddNetLengthOnBottomOfDriverInst  1
  set lineExpToSplitPath                  {^TE} ; # used to regexp
  set stdcellExp                          {.*D\d+BWP(LVT)?} ; # don't use char '^' or '$'
  set startOfPath                         {Point\s+} ; # end expression of launch timing path
  set endOfPath                           {data arrival time} ; # end expression of launch timing path
  # for genCmd_highlightTimingPathBasedOnListOfEvenNumberedItems
  set modeOfConnect                       "whole_net" ; # whole_net|flight_line
  set ifWithArrow                         1; # 1|0
  set colorsIndexLoopListsForNet          {60 50 62 63 61 55 52 4 6 14 15 17 28 29 31 56 57 61 64 42} ; # 20 items
  set colorsIndexLoopListsForInst         {33 34 35 36 37 38 39 40 41 42 43 44 45 46 47 48 33 34 35 36} ; # 20 items
  set layerOfText                         8
  set indexOfPureNumberOnLine             "end-1"
  set indexOfAfterSplitOriginalLineList   "0"
  parse_proc_arguments -args $args opt
  foreach arg [array names opt] {
    regsub -- "-" $arg "" var
    set $var $opt($arg)
  }
  set evenNumberLists_instName_instBox2_3_incrCellDelay [genCmd_getPurePinOfPath_fromTimingPathReport -reportTimingFile $reportTimingFile -lineExpToSplitPath $lineExpToSplitPath \
                        -stdcellExp $stdcellExp -startOfPath $startOfPath -endOfPath $endOfPath -indexOfPureNumberOnLine $indexOfPureNumberOnLine \
                        -indexOfAfterSplitOriginalLineList $indexOfAfterSplitOriginalLineList]
  set cmdsList [list]
  set i 0
  foreach tempEvenNumberList [lindex $evenNumberLists_instName_instBox2_3_incrCellDelay 0] {
    set tempCmdsList [genCmd_highlightTimingPathBasedOnListOfEvenNumberedItems -evenNumberList $tempEvenNumberList -modeOfConnect $modeOfConnect -ifWithArrow $ifWithArrow \
                          -colorsIndexLoopListsForNet $colorsIndexLoopListsForNet -colorsIndexLoopListsForInst $colorsIndexLoopListsForInst -indexOfColorsForNetInst $i]
    set tempAddCellTypeOnTopOfInstRect [list]
    if {$ifAddCellTypeOnTopOfInstRect} {
      set tempAddCellTypeOnTopOfInstRect [genCmd_addCellTypeOnTopOfInstRect -evenNumberList $tempEvenNumberList -layerOfText $layerOfText]
    }
    if {$ifAddNetLengthOnBottomOfDriverInst} {
      set tempAddNetLengthOnBottomOfDriverInst [genCmd_addTextOfNetLengthOnBottomOfDriverInst -layerOfText $layerOfText -evenNumberList $tempEvenNumberList]
    }
    incr i
    lappend cmdsList {*}$tempCmdsList {*}$tempAddCellTypeOnTopOfInstRect {*}$tempAddNetLengthOnBottomOfDriverInst
  }
  if {$ifAddMarkersAndText} {
    foreach temp_instName_instBox2_3_incrCellDelay [lindex $evenNumberLists_instName_instBox2_3_incrCellDelay 1] {
      lassign $temp_instName_instBox2_3_incrCellDelay temp_instname temp_instbox2_3 temp_incrCellDelay
      lappend cmdsList "add_gui_text -box \{$temp_instbox2_3\} -layer $layerOfText -label \"cell_dly:$temp_incrCellDelay\" -no_bbox"
    }
  }
  return $cmdsList
}

define_proc_arguments genCmd_highlightTimingPathBasedOnReportFile \
  -info "gen cmd for highlighting timing path based on reprot file"\
  -define_args {
    {-reportTimingFile "specify the file name of report_timing" AString string optional}
    {-ifAddMarkersAndText "if add Markers and Text for path" oneOfString one_of_string {optional value_type {values {1 0}}}}
    {-lineExpToSplitPath "specify expression of spliting timing path" AString string optional}
    {-stdcellExp "specify the expression of match stdcell name(of LibCells)" AString string optional}
    {-startOfPath "specify the start expression of timing path" AString string optional}
    {-endOfPath "specify the end expression of timing path" AString string optional}
    {-modeOfConnect "specify the type of eco" oneOfString one_of_string {optional value_type {values {whole_net flight_line}}}}
    {-ifWithArrow "if using arrow on line" oneOfString one_of_string {optional value_type {values {1 0}}}}
    {-colorsIndexLoopListsForNet "specify the colors index loop lists for net color" AList list optional}
    {-colorsIndexLoopListsForInst "specify the colors index loop lists for inst color" AList list optional}
    {-layerOfText "specify the layer of text that need add to" AString string optional}
    {-indexOfPureNumberOnLine "When the content in the 'line' becomes pure numbers (with non-numeric parts removed), the number of items in 'delay'" AString string optional}
    {-indexOfAfterSplitOriginalLineList "specify the index of list after spliting original lines" AInt int optional}
  }

#!/bin/tclsh
# --------------------------
# author    : sar song
# date      : 2025/09/25 12:54:25 Thursday
# label     : gui_proc
#   tcl  -> (atomic_proc|display_proc|gui_proc|task_proc|dump_proc|check_proc|math_proc|package_proc|test_proc|datatype_proc|db_proc|flow_proc|report_proc|cross_lang_proc|misc_proc)
#   perl -> (format_sub|getInfo_sub|perl_task)
# descrip   : Obtain the shortest net length for a pin to reach the next pin based on the pin's name, and use the add_gui_text command to add text.
# return    : cmds list
# ref       : link url
# --------------------------
proc genCmd_addTextOfNetLengthOnBottomOfDriverInst {args} {
  set layerOfText    8
  set evenNumberList {}
  parse_proc_arguments -args $args opt
  foreach arg [array names opt] {
    regsub -- "-" $arg "" var
    set $var $opt($arg)
  }
  if {[llength $evenNumberList] && ![expr {[llength $evenNumberList] % 2}]} {
    set cmdsList [list]
    foreach {pin1 pin2} $evenNumberList {
      set net1 [dbget [dbget top.insts.instTerms.name $pin1 -p].net.name -e] 
      set net2 [dbget [dbget top.insts.instTerms.name $pin2 -p].net.name -e] 
      if {$net1 == $net2 && $net1 != ""} {
        set outNetLen [dict get [reportWirePath -start $pin1 -end $pin2 -tcl -no_output] total_length]
        set inst1_rect {*}[dbget [dbget top.insts.name [join [lrange [split $pin1 "/"] 0 end-1] "/"] -p].box -e]
        set inst1_rect_3_3 [lset inst1_rect end [expr {([lindex $inst1_rect 3] - [lindex $inst1_rect 1]) / 3 + [lindex $inst1_rect 1]}]]
        lappend cmdsList "add_gui_text -layer $layerOfText -box \{$inst1_rect_3_3\} -no_bbox -label \"outLen:[format "%.1f" $outNetLen]\""
      } else {
        error "proc genCmd_addTextOfNetLengthOnBottomOfDriverInst: check your input : pin1($pin1) and pin2($pin2) of evenNumberList is not on same net!!!" 
      }
    }
    return $cmdsList
  } else {
    error "proc genCmd_addTextOfNetLengthOnBottomOfDriverInst: check your input: evenNumberList($evenNumberList) is empty or is not even-number!!!" 
  }
}

define_proc_arguments genCmd_addTextOfNetLengthOnBottomOfDriverInst \
  -info "gen cmd for adding text of net length on center of net"\
  -define_args {
    {-layerOfText "specify the layer to add text" AString string optional}
    {-evenNumberList "specify the list that is even-number" AList list optional}
  }

#!/bin/tclsh
# --------------------------
# author    : sar song
# date      : 2025/09/25 00:06:02 Thursday
# label     : gui_proc
#   tcl  -> (atomic_proc|display_proc|gui_proc|task_proc|dump_proc|check_proc|math_proc|package_proc|test_proc|datatype_proc|db_proc|flow_proc|report_proc|cross_lang_proc|misc_proc)
#   perl -> (format_sub|getInfo_sub|perl_task)
# descrip   : Get the cell type of the 'inst' and add text to this 'inst'.
# return    : cmds list
# ref       : link url
# --------------------------
proc genCmd_addCellTypeOnTopOfInstRect {args} {
  set evenNumberList {}
  set layerOfText    8
  parse_proc_arguments -args $args opt
  foreach arg [array names opt] {
    regsub -- "-" $arg "" var
    set $var $opt($arg)
  }
  if {[llength evenNumberList]} {
    set uniqueInstNameList [lsort -unique [lmap temp_pin $evenNumberList {
      set temp_inst [join [lrange [split $temp_pin "/"] 0 end-1] "/"]
    }]]
    set inst_celltype_box_list [lmap temp_inst $uniqueInstNameList {
      set temp_celltype [dbget [dbget top.insts.name $temp_inst -p].cell.name -e]
      if {$temp_celltype == ""} {
        error "proc genCmd_addCellTypeOnTopOfInstRect: check your input : evenNumberList($evenNumberList) have invalid pin name(inst name : $temp_inst) !!!"
      }
      set temp_box {*}[dbget [dbget top.insts.name $temp_inst -p].box -e]
      set temp_box_1_3 [lset temp_box 1 [expr {([lindex $temp_box end] - [lindex $temp_box 1]) * 2 / 3 + [lindex $temp_box 1]}]]
      list $temp_inst $temp_celltype $temp_box_1_3
    }]
    set cmdsList [lmap temp_inst_celltype_box $inst_celltype_box_list {
      lassign $temp_inst_celltype_box temp_inst temp_celltype temp_box
      set temp "add_gui_text -box \{$temp_box\} -label $temp_celltype -layer $layerOfText -no_bbox"
    }]
    return $cmdsList
  } else {
    error "proc genCmd_addCellTypeOnTopOfInstRect: check your input : evenNumberList($evenNumberList) is empty!!!"
  }
}
define_proc_arguments genCmd_addCellTypeOnTopOfInstRect \
  -info "gen cmd for adding celltype on top of inst rect"\
  -define_args {
    {-evenNumberList "specify the even number list" AList list optional}
    {-layerOfText "specify the layer to add text" AString string optional}
  }

#!/bin/tclsh
# --------------------------
# author    : sar song
# date      : 2025/09/23 23:14:45 Tuesday
# label     : gui_proc
#   tcl  -> (atomic_proc|display_proc|gui_proc|task_proc|dump_proc|check_proc|math_proc|package_proc|test_proc|datatype_proc|db_proc|flow_proc|report_proc|cross_lang_proc|misc_proc)
#   perl -> (format_sub|getInfo_sub|perl_task)
# descrip   : According to the input list of even-numbered items, each item is a pin name, and every two items form a pair. Pins in a pair must be on the same net. If they are 
#             not on the same net, an error message will be triggered. You can choose to use functions like "flight line" or "whole_net" to highlight the connection relationship, 
#             which will be accompanied by arrows indicating the direction. It is possible to customize a circular list of highlight colors (including colors for nets and insts), 
#             and you can specify whether to highlight insts or nets.
# return    : cmds list
# ref       : link url
# --------------------------
source ../packages/logic_AND_OR.package.tcl; # eo
proc genCmd_highlightTimingPathBasedOnListOfEvenNumberedItems {args} {
  set evenNumberList     {} ; # must be even number list
  set modeOfConnect "whole_net" ; # whole_net|flight_line
  set ifWithArrow   1; # 1|0
  set colorsIndexLoopListsForNet {60 50 62 63 61 55 52 4 6 14 15 17 28 29 31 56 57 61 64 42} ; # 20 items
  set colorsIndexLoopListsForInst {1 2 3 5 7 9 10 11 14 15 17 19 20 21 24 22 25 28 30 32} ; # 20 items
  set indexOfColorsForNetInst 0 ; # 0-19
  parse_proc_arguments -args $args opt
  foreach arg [array names opt] {
    regsub -- "-" $arg "" var
    set $var $opt($arg)
  }
  set indexOfColorsForNetInst [expr {$indexOfColorsForNetInst % [expr {([llength $colorsIndexLoopListsForNet] + [llength $colorsIndexLoopListsForInst]) / 2 }]}]
  if {[expr {[llength $evenNumberList] % 2}]} {
    error "proc genCmd_highlightTimingPathBasedOnListOfEvenNumberedItems: check your evenNumberListï¼ˆnum: [llength $evenNumberList]) must be even number list!!!" 
  }
  set hiliteCmdsList [list ]
  foreach {pin1 pin2} $evenNumberList {
    set pin1_net [dbget [dbget top.insts.instTerms.name $pin1 -p].net.name -e] 
    set pin2_net [dbget [dbget top.insts.instTerms.name $pin2 -p].net.name -e]
    if {$pin1_net == "" || $pin2_net == ""} {
      error "proc genCmd_highlightTimingPathBasedOnListOfEvenNumberedItems: check your input: pin($pin1_net or $pin2_net) is not net to connect!!!" 
    } else {
      if {$pin1_net != $pin2_net} {
        error "proc genCmd_highlightTimingPathBasedOnListOfEvenNumberedItems: check your input: both pins($pin1_net and $pin2_net) are not on same net!!!" 
      } else {
        set netColor [lindex $colorsIndexLoopListsForNet $indexOfColorsForNetInst]
        set instColor [lindex $colorsIndexLoopListsForInst $indexOfColorsForNetInst]
        lappend hiliteCmdsList "highlight_pin_connection -from_pin $pin1 -to_pin $pin2 -mode $modeOfConnect [eo $ifWithArrow "-with_arrow" ""] -net_color_index $netColor -inst_color_index $instColor"
      }
    }
  }
  return $hiliteCmdsList
}

define_proc_arguments genCmd_highlightTimingPathBasedOnListOfEvenNumberedItems \
  -info "gen cmd for highlighting timing path based on list of even-numbered items"\
  -define_args {
    {-modeOfConnect "specify the type of eco" oneOfString one_of_string {optional value_type {values {whole_net flight_line}}}}
    {-evenNumberList "specify inst to eco when type is add/delete" AList list optional}
    {-ifWithArrow "if using arrow on line" oneOfString one_of_string {optional value_type {values {1 0}}}}
    {-colorsIndexLoopListsForNet "specify the colors index loop lists for net color" AList list optional}
    {-colorsIndexLoopListsForInst "specify the colors index loop lists for inst color" AList list optional}
    {-indexOfColorsForNetInst "specify the index of Net and Inst color, it can get index with circle" AInt int optional}
  }


#!/bin/tclsh
# --------------------------
# author    : sar song
# date      : 2025/09/24 15:12:23 Wednesday
# label     : misc_proc
#   tcl  -> (atomic_proc|display_proc|gui_proc|task_proc|dump_proc|check_proc|math_proc|package_proc|test_proc|datatype_proc|db_proc|flow_proc|report_proc|cross_lang_proc|misc_proc)
#   perl -> (format_sub|getInfo_sub|perl_task)
# descrip   : Obtain the path pins that need to be highlighted from the report_timing (rpt) file of PT, and different paths can be automatically split based on keywords.
# return    : lists of even pins
# ref       : link url
# --------------------------
source ./common/split_timing_path.common.tcl; # split_timing_path
proc genCmd_getPurePinOfPath_fromTimingPathReport {args} {
  set reportTimingFile                  ""
  set indexOfPureNumberOnLine           "end-1"
  set indexOfAfterSplitOriginalLineList "0"
  set lineExpToSplitPath                {^TE} ; # used to regexp
  set stdcellExp                        {.*D\d+BWP(LVT)?} ; # don't use char '^' or '$'
  set startOfPath                       {Point\s+} ; # end expression of launch timing path
  set endOfPath                         {data arrival time} ; # end expression of launch timing path
  parse_proc_arguments -args $args opt
  foreach arg [array names opt] {
    regsub -- "-" $arg "" var
    set $var $opt($arg)
  }
  if {![file isfile $reportTimingFile]} {
    error "proc genCmd_getPurePinOfPath_fromTimingPathReport: check your input: file name ($reportTimingFile) is not found!!!" 
  }
  set fi [open $reportTimingFile r]
  set content [split [read $fi] \n]
  set stdcellExpToMatchLine [subst -nocommands -nobackslashes {($stdcellExp)}]
  set pinLines [lmap templine $content {
    if {[regexp $lineExpToSplitPath|$stdcellExpToMatchLine|$startOfPath|$endOfPath $templine]} {
      set temp $templine 
    }
  }]
  set purePinListWithSplitLine [lmap templine $pinLines {
    if {[regexp {^\s*$} $templine]} { continue }
    set indexOfStdCell [lsearch -regexp $templine $stdcellExpToMatchLine]
    if {$indexOfStdCell == -1} { 
      if {[regexp $startOfPath $templine]} { 
        set temp "START" 
      } elseif {[regexp $endOfPath $templine]} {
        set temp "END" 
      } elseif {[regexp $lineExpToSplitPath $templine]} {
        set temp "SPLIT"
      } else {
        continue
      }
    } else {
      set pinName [lindex $templine [expr {$indexOfStdCell - 1}]]
    }
  }]
  set originalLineListWithSplitLine [lmap templine $pinLines {
    if {[regexp {^\s*$} $templine]} { continue }
    set indexOfStdCell [lsearch -regexp $templine $stdcellExpToMatchLine]
    if {$indexOfStdCell == -1} { 
      if {[regexp $startOfPath $templine]} { 
        set temp "START" 
      } elseif {[regexp $endOfPath $templine]} {
        set temp "END" 
      } elseif {[regexp $lineExpToSplitPath $templine]} {
        set temp "SPLIT"
      } else {
        continue
      }
    } else {
      set originalLine $templine
    }
  }]
  set splitedPinList [split_timing_path $purePinListWithSplitLine]
  set splitedOriginalList [split_timing_path $originalLineListWithSplitLine]
  set filteredPinList [lmap tempList $splitedPinList {
    if {[llength $tempList] == 1} { continue } else {
      if {[join [lrange [split [lindex $tempList 0] "/"] 0 end-1] "/"] eq [join [lrange [split [lindex $tempList 1] "/"] 0 end-1] "/"]} {
        set tempList [lrange $tempList 1 end] 
      }
      if {[expr {[llength $tempList] % 2}]} {
        error "proc genCmd_highlightTimingPathBasedOnListOfEvenNumberedItems: check your path([join $tempList " - > "]) need be even number list(length: [llength $tempList]) !!!(in filteredPinList)" 
      }
      foreach {pin1 pin2} $tempList {
        set net1 [dbget [dbget top.insts.instTerms.name $pin1 -p].net.name -e]
        set net2 [dbget [dbget top.insts.instTerms.name $pin2 -p].net.name -e]
        if {$net1 == "" || $net2 == ""} {
          error "proc genCmd_getPurePinOfPath_fromTimingPathReport: check your pinname($pin1 or $pin2) in rpt file($reportTimingFile), not found net!!! (in filteredPinList)" 
        }
        if {$net1 != $net2} {
          error "proc genCmd_getPurePinOfPath_fromTimingPathReport: both pins($pin1 and $pin2) are not on same net(net1: $net1 , net2: $net2)!!! (in filteredPinList)"
        }
      }
    }
    set temp $tempList
  }]
  set filteredOriginalLineList [lmap tempList $splitedOriginalList {
    if {[llength $tempList] == 1} { continue } else {
      if {[join [lrange [split [lindex $tempList 0 $indexOfAfterSplitOriginalLineList] "/"] 0 end-1] "/"] eq [join [lrange [split [lindex $tempList 1 $indexOfAfterSplitOriginalLineList] "/"] 0 end-1] "/"]} {
        set tempList [lrange $tempList 1 end]
      }
      if {[expr {[llength $tempList] % 2}]} {
        error "proc genCmd_highlightTimingPathBasedOnListOfEvenNumberedItems: check your path(\n[join $tempList "\n - > "]) need be even number list(length: [llength $tempList]) !!!(in filteredOriginalLineList)" 
      }
      foreach {line1 line2} $tempList {
        set pin1 [lindex $line1 $indexOfAfterSplitOriginalLineList]
        set pin2 [lindex $line2 $indexOfAfterSplitOriginalLineList]
        set net1 [dbget [dbget top.insts.instTerms.name $pin1 -p].net.name -e]
        set net2 [dbget [dbget top.insts.instTerms.name $pin2 -p].net.name -e]
        if {$net1 == "" || $net2 == ""} {
          error "proc genCmd_getPurePinOfPath_fromTimingPathReport: check your pinname($pin1 or $pin2) in rpt file($reportTimingFile), not found net!!!(in filteredOriginalLineList)" 
        }
        if {$net1 != $net2} {
          error "proc genCmd_getPurePinOfPath_fromTimingPathReport: both pins($pin1 and $pin2) are not on same net(net1: $net1 , net2: $net2)!!!(in filteredOriginalLineList)"
        }
      }
    }
    set temp $tempList
  }]
  set instName_instBox2_3_incrCellDelay [list]
  set j 0
  foreach temp_sub_list $filteredPinList {
    foreach {pin1 pin2} $temp_sub_list {
      set temp_line [lsearch -regexp -inline [lindex $filteredOriginalLineList $j] [subst -nocommands -nobackslashes {.*$pin1.*}]]
      if {$temp_line == ""} {
        error "proc genCmd_getPurePinOfPath_fromTimingPathReport: check your input : pin1($pin1) can't find matched line in ([lindex $splitedOriginalList $j])!!!"
      }
      set pureNumberLineList [lmap temp_item $temp_line {
        if {[string is double $temp_item]} {
          set temp $temp_item
        } else {
          continue
        }
      }]
      set incr_delay [lindex $pureNumberLineList $indexOfPureNumberOnLine]
      set temp_inst [join [lrange [split $pin1 "/"] 0 end-1] "/"]
      set temp_inst_box {*}[dbget [dbget top.insts.name $temp_inst -p].box -e]
      set temp_inst_box_2_3 [lset temp_inst_box 1 [expr {([lindex $temp_inst_box end] - [lindex $temp_inst_box 1]) / 3 + [lindex $temp_inst_box 1]}]]
      set temp_inst_box_2_3 [lset temp_inst_box_2_3 end [expr {([lindex $temp_inst_box end] - [lindex $temp_inst_box 1]) / 3 + [lindex $temp_inst_box 1]}]]
      lappend instName_instBox2_3_incrCellDelay [list $temp_inst $temp_inst_box_2_3 $incr_delay]
    }
    incr j
  }
  return [list $filteredPinList $instName_instBox2_3_incrCellDelay]
}

define_proc_arguments genCmd_getPurePinOfPath_fromTimingPathReport \
  -info "gen cmd for geting pure pin of path from timing path report, \n\t \
  Notice: This proc uses the report_timing rpt file generated by PT to match and \
  obtain pin names. It must display both the input pin and output pin names of the \
  inst, and must not only show the output pin name. In addition, after the pin name, \
  there must be a number of spaces followed by the format '(stdCellName)', because \
  the proc uses this positional relationship to obtain the pin name."\
  -define_args {
    {-reportTimingFile "specify the file name of report_timing" AString string optional}
    {-indexOfPureNumberOnLine "When the content in the 'line' becomes pure numbers (with non-numeric parts removed), the number of items in 'delay'" AString string optional}
    {-indexOfAfterSplitOriginalLineList "specify the index of list after spliting original lines" AInt int optional}
    {-lineExpToSplitPath "specify expression of spliting timing path" AString string optional}
    {-stdcellExp "specify the expression of match stdcell name(of LibCells)" AString string optional}
    {-startOfPath "specify the start expression of timing path" AString string optional}
    {-endOfPath "specify the end expression of timing path" AString string optional}
  }
