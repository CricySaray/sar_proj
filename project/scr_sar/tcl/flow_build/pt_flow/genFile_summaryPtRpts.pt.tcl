#!/bin/tclsh
# --------------------------
# author    : sar song
# date      : 2025/09/28 11:18:19 Sunday
# label     : flow_proc
#   tcl  -> (atomic_proc|display_proc|gui_proc|task_proc|dump_proc|check_proc|math_proc|package_proc|test_proc|datatype_proc|db_proc|flow_proc|report_proc|cross_lang_proc|misc_proc)
#   perl -> (format_sub|getInfo_sub|perl_task)
# descrip   : Generate the specified report using PT-specific commands, and then execute this proc in PT (where the define_proc_attributes command can only be executed in PT). 
#             Summarize one or several scenarios and create a table, then generate the table into a file for easy reading. It can also be written into the PT flow, generating 
#             a separate table file for each scenario. After all scenarios are executed, use a simple file aggregation to integrate the data from each scenario into a master 
#             table. In this way, data can be saved layer by layer.
# usage     : 
# NOTICE    : 1) please generate constraint report file using command: report_constraint -pba_mode $pba_mode -sinificant_digits 3 -all_violator -path_type end|slack_only -nos -max_delay -min_delay -max_transition -max_capacitance -max_fanout -min_period -min_pulse_width -pba_path path|exhaustive
#             2) generate annotated report file using report_annotated_parasitics -check -pin_to_pin_nets
#             3) generate global timing csv file using report_global_timing -pab_mode $pba_mode -significant_digits 3 -format csv -output outputfile.name
# return    : csv summary file
# ref       : link url
# --------------------------
# small tool to collect these csv file of summary generated by proc: runCmd_summarize_pt_rpts, you can run this seperately
source ../common/generate_combinations.common.tcl; # generate_combinations
source ../packages/table_format_with_title.package.tcl; # table_format_with_title
# NOTICE: you may need this proc: lmap implementation proc in tcl 8.5 when your tcl version is lower than tcl 8.6 : ../packages/lmap_implementation_on_Tcl8.5.package.tcl
proc collect_sum_csv_file {args} {
  set searchDir              "./"
  set formatOfScenarios      "<mode>_<type>_<voltage>_<rcCorner>_<temperature>"
  set modesOfFormatExp       {func scan}
  set typeToCheckOfFormatExp {setup hold}
  set voltageOfFormatExp     {0p99v 1p1v 1p21v}
  set rcCornerOfFormatExp    {cworst cbest rcworst rcbest typical}
  set temperatureOfFormatExp {25c m40c 125c}
  parse_proc_arguments -args $args opt
  foreach arg [array names opt] {
    regsub -- "-" $arg "" var
    set $var $opt($arg)
  }

  set optionsOfFormatOfScenarios [list "<mode>" "<type>" "<voltage>" "<rcCorner>" "<temperature>"] ; # you only select options inside these
  set mode $modesOfFormatExp ; set type $typeToCheckOfFormatExp ; set voltage $voltageOfFormatExp ; set rcCorner $rcCornerOfFormatExp ; set temperature $temperatureOfFormatExp
  set allCasesOfScenarios [generate_combinations -connector "_" {*}[lmap temp_var [regsub -all {>|<} $optionsOfFormatOfScenarios ""] { if {![llength [subst \${$temp_var}]]} {error "proc collect_sum_csv_file: check your input: the $temp_var is empty!!!"} else {set $temp_var}}]]
  if {![file isdirectory $searchDir]} {
    error "proc collect_sum_csv_file: check your input: searchDir($searchDir) does not exist!!!" 
  }
  set allDirNameOnSearchDir [lmap temp_path [glob -nocomplain $searchDir/*] { file tail $temp_path }]
  set scenarioDirs [lmap temp_dirname $allDirNameOnSearchDir { if {$temp_dirname in $allCasesOfScenarios} {set temp_dirname} else {continue} }]
  if {$scenarioDirs == ""} { error "proc collect_sum_csv_file: check your input: scenarios dir on searching directory is empty!!!" }
  set all_content [list ]
  foreach temp_scenario $scenarioDirs {
    if {[file isfile "$temp_scenario/sum.csv"]} {
      set temp_fi [open "$temp_scenario/sum.csv" r] ; set temp_content [split [read $temp_fi] "\n"] ; close $temp_fi
      set final_title [lindex $temp_content 0]
      lappend all_content [lindex $temp_content 1]
    } else {
      puts "Notice : dir: $temp_scenario have no sum.csv file!!!"
    }
  }
  set all_content [lsort -index 0 -ascii $all_content]
  # M = max, m = min, MfanW = max fanout WNS, mPrdN = min period NUM, mPulW = min pulse width WNS, Nanno = not annotated pin to pin nets
  set infoOfAllScenarios [linsert [list] 0 [list scenario wns num tns r2r_w r2r_n r2r_t tranW tranN MfanW MfanN MCapW MCapN mPrdW mPrdN mPulW mPulN Nanno]]
  set alignMethodsList [list left right center right right center right right center right center right center right center right center center]
  set table_of_whole_sum_csv [join [table_format_with_title [concat [list $final_title] $all_content] 0 $alignMethodsList "" 0] \n]
  set fo [open "$searchDir/sum.csv" w]
  puts $fo $table_of_whole_sum_csv
  close $fo
}
define_proc_arguments collect_sum_csv_file \
  -info "collect sum csv files when all scenarios run done, and every scenario's sum.csv has generated." \
  -define_args {
    {-searchDir "specify the searching directory for collecting" AString string optional}
    {-formatOfScenarios "specify the format of scenarios, that will be used to match every dir to collect scenario dir" AString string optional}
    {-modesOfFormatExp "specify the modes(like: func scan) when specifying format of scenarios" AList list optional}
    {-typeToCheckOfFormatExp "specify the type(only be setup or hold) when specifying format of scenarios" AList list optional }
    {-voltageOfFormatExp "specify the voltage(like: 1p1v 1p21v 0p99v) when specifying format of scenarios" AList list optional }
    {-rcCornerOfFormatExp "specify the rc corners(like: cworst cbest rcbest rcworst typical) when specifying format of scenarios" AList list optional}
    {-temperatureOfFormatExp "specify the temperature(like: 0c m40c 85c 125c) when specifying format of scenarios" AList list optional} 
  }


source ../common/generate_combinations.common.tcl; # generate_combinations
source ../common/parse_constraint_report.common.tcl; # parse_constraint_report 
source ../packages/table_format_with_title.package.tcl; # table_format_with_title 
proc runCmd_summarize_pt_rpts {args} {
  set searchDir                   "./" ; # for example: /simulation/arsong/SC5019/PT/run/V0926_S0926_FP0926_0926_100_dft_v3_eco1
  set outputFileOfSummary         "$searchDir/<scenario>/sum.csv" ; # if have '<scenario>' in this path, this will write output for every scenario dir
  set scenarios                   "auto" ; # auto (will search ) or [list ...], for example: func_hold_1p21v_rcbest_125c
  set formatOfScenarios           "<mode>_<type>_<voltage>_<rcCorner>_<temperature>" ; # format instance like: func_setup_0p99v_rcworst_m40c
  set modesOfFormatExp            {func scan}
  set typeToCheckOfFormatExp      {setup hold}
  set voltageOfFormatExp          {0p99v 1p1v 1p21v}
  set rcCornerOfFormatExp         {cworst cbest rcworst rcbest typical}
  set temperatureOfFormatExp      {25c m40c 125c}
  set designName                  "SC5019_TOP" ; # only consisted of reportConstraintFileName
  set reportConstraintFileName    "report_constraint_$designName.rpt"
  set globalTimingCsvFileName     "${designName}.global_timing.csv" ; # please using command: report_global_timing -format csv -ouput .../$designName.global_timing.csv
  set annotatedParasiticsFileName "report_annotated_parasitics.rpt"
  set outputDirOfViolList         "$searchDir/<scenario>/"
  set prefixOutPutFile            "sor_${designName}_"
  set waive_list_file             "" ; # please provided it by yourself
  set waive_prefix_prompt_list    {{max_delay SETUP} {min_delay HOLD} {max_transition TRAN} {max_capacitance CAP} {max_fanout FANOUT} {min_pulse_width PULSE} {min_period PERIOD}}
  parse_proc_arguments -args $args opt
  foreach arg [array names opt] {
    regsub -- "-" $arg "" var
    set $var $opt($arg)
  }
  if {$scenarios == "auto"} {
    set optionsOfFormatOfScenarios [list "<mode>" "<type>" "<voltage>" "<rcCorner>" "<temperature>"] ; # you only select options inside these
    set mode $modesOfFormatExp ; set type $typeToCheckOfFormatExp ; set voltage $voltageOfFormatExp ; set rcCorner $rcCornerOfFormatExp ; set temperature $temperatureOfFormatExp
    set allCasesOfScenarios [generate_combinations -connector "_" {*}[lmap temp_var [regsub -all {>|<} $optionsOfFormatOfScenarios ""] { if {![llength [subst \${$temp_var}]]} {error "proc runCmd_summarize_pt_rpts: check your input: the $temp_var is empty!!!"} else {set $temp_var}}]]
  } else {
    set allCasesOfScenarios $scenarios 
  }
  if {![file isdirectory $searchDir]} {
    error "proc runCmd_summarize_pt_rpts: check your input: searchDir($searchDir) does not exist!!!" 
  }
  set allDirNameOnSearchDir [lmap temp_path [glob -nocomplain $searchDir/*] { file tail $temp_path }]
  set scenarioDirs [lmap temp_dirname $allDirNameOnSearchDir { if {$temp_dirname in $allCasesOfScenarios} {set temp_dirname} else {continue} }]
  if {$scenarioDirs == ""} { error "proc runCmd_summarize_pt_rpts: check your input: scenarios dir on searching directory is empty!!!" }
  set allConstraintNameList [list min_delay max_delay max_capacitance max_transition max_fanout min_pulse_width min_period]

  set infoOfAllScenarios [lmap temp_scenario_dir $scenarioDirs {
    set splitedOptionOfScenario [regexp -all -inline {[a-z0-9]+} $temp_scenario_dir]
    if {[lsearch -exact $splitedOptionOfScenario "setup"] != -1} { set typeOfScenario setup } elseif {[lsearch -exact $splitedOptionOfScenario "hold"] != -1} { set typeOfScenario hold }
    if {[regexp {<scenario>} $outputDirOfViolList]} { set outputDirOfViolList [string map [list <scenario> $temp_scenario_dir] $outputDirOfViolList] }
    set constraintNameWnsNumLists [parse_constraint_report "$searchDir/$temp_scenario_dir/$reportConstraintFileName" $waive_list_file $waive_prefix_prompt_list $outputDirOfViolList $prefixOutPutFile 0] ; # have item include "NA" like {max_delay NA NA}, you can add other actions for it NOTICE
    foreach temp_constaint_wns_num $constraintNameWnsNumLists {
      lassign $temp_constaint_wns_num temp_const temp_wns temp_num
      if {$temp_const in $allConstraintNameList} {
        set wns_$temp_const $temp_wns
        set num_$temp_const $temp_num
      }
    }
    if {$typeOfScenario == "setup"} {
      set wns_type_delay $wns_max_delay ; set num_type_delay $num_max_delay ; set expOfGlobalTimingCsv {^max,}
    } elseif {$typeOfScenario == "hold"} {
      set wns_type_delay $wns_min_delay ; set num_type_delay $num_min_delay ; set expOfGlobalTimingCsv {^min,}
    } else {
      error "proc runCmd_summarize_pt_rpts: check your formatOfScenarios($formatOfScenarios), not find 'setup' or 'hold' !!!"
    }
    set fi_temp [open "$searchDir/$temp_scenario_dir/$globalTimingCsvFileName" r] ; set globalTimingContent [split [read $fi_temp] "\n"] ; close $fi_temp
    set titleList [split [lindex $globalTimingContent 0] ","]
    set indexOfTypeOnGlobalTimingContent [lsearch -regexp $globalTimingContent $expOfGlobalTimingCsv]
    if {$indexOfTypeOnGlobalTimingContent == -1} {
      set totalTNS 0 ; set reg2regWNS 0 ; set reg2regTNS 0 ; set reg2regNUM 0 
    } else {
      set valueList [split [lindex $globalTimingContent $indexOfTypeOnGlobalTimingContent] ","]
      set totalTNS [lindex $valueList [lsearch -exact $titleList "Total_TNS"]]
      set reg2regWNS [lindex $valueList [lsearch -exact $titleList "reg2reg_WNS"]]
      set reg2regTNS [lindex $valueList [lsearch -exact $titleList "reg2reg_TNS"]]
      set reg2regNUM [lindex $valueList [lsearch -exact $titleList "reg2reg_NUM"]]
    }
    set fi_annotated [open "$searchDir/$temp_scenario_dir/$annotatedParasiticsFileName" r] ; set annotatedParasiticsContent [split [read $fi_annotated] "\n"] ; close $fi_annotated
    set pin_to_pin_line [lsearch -inline -regexp $annotatedParasiticsContent {^\s+- Pin to pin nets .*}]
    set num_not_annotated [lindex [lmap temp_item $pin_to_pin_line { if {[string is double $temp_item]} {set temp $temp_item} else {continue} }] end]
    list $temp_scenario_dir $wns_type_delay $num_type_delay $totalTNS $reg2regWNS $reg2regNUM $reg2regTNS $wns_max_transition $num_max_transition $wns_max_fanout $num_max_fanout $wns_max_capacitance $num_max_capacitance $wns_min_period $num_min_period $wns_min_pulse_width $num_min_pulse_width $num_not_annotated
  }]
  # M = max, m = min, MfanW = max fanout WNS, mPrdN = min period NUM, mPulW = min pulse width WNS, Nanno = not annotated pin to pin nets
  set infoOfAllScenarios [linsert $infoOfAllScenarios 0 [list scenario wns num tns r2r_w r2r_n r2r_t tranW tranN MfanW MfanN MCapW MCapN mPrdW mPrdN mPulW mPulN Nanno]]
  set tableToDisplay [join [table_format_with_title $infoOfAllScenarios 0 center "" 0] \n]
  if {[regexp {<scenario>} $outputFileOfSummary]} { 
    set allOutputFileOfSummary [lmap temp_scenario $allCasesOfScenarios { 
      set temp_output_file_of_summary [string map [list <scenario> $temp_scenario] $outputFileOfSummary]
      if {![file isdirectory [file dirname $temp_output_file_of_summary]]} {
        error "proc runCmd_summarize_pt_rpts: check your input : dir name of outputFileOfSummary($outputFileOfSummary) is not found!!!" 
      } else {
        set temp_output_file_of_summary 
      }
    }]
  } else {
    if {![file isdirectory [file dirname $outputFileOfSummary]]} {
      error "proc runCmd_summarize_pt_rpts: check your input : dir name of outputFileOfSummary($outputFileOfSummary) is not found!!!" 
    }
    set allOutputFileOfSummary $outputFileOfSummary
  }
  foreach temp_file_to_output $allOutputFileOfSummary {
    set fo [open "$temp_file_to_output" w]
    puts $fo $tableToDisplay
    close $fo
  }
}

define_proc_attributes runCmd_summarize_pt_rpts \
  -info "run cmd of summarizing pt rpts"\
  -define_args {
    {-searchDir "specify the dir to search" AString string optional}
    {-outputFileOfSummary "specify the output file of summary" AString string optional}
    {-scenarios "you can specify some scenarios name list or 'auto' that is can match all possible scenarios" AList list optional}
    {-formatOfScenarios "specify the format of scenarios if \$scenarios == 'auto'" AString string optional}
    {-modesOfFormatExp "specify the modes of Format Exp if \$scenarios == 'auto'" AList list optional}
    {-typeToCheckOfFormatExp "specify the type to check of Format Exp if \$scenarios == 'auto'" AList list optional}
    {-voltageOfFormatExp "specify the voltage of Format Exp if \$scenarios == 'auto'" AList list optional}
    {-rcCornerOfFormatExp "spcify the rc corners of Format Exp if \$scenarios == 'auto'" AList list optional}
    {-temperatureOfFormatExp "specify the temperature of Format Exp if \$scenarios == 'auto'" AList list optional}
    {-designName "specify the design name" AString string optional}
    {-reportConstraintFileName "specify the report_constraint file name" AString string optional}
    {-globalTimingCsvFileName "specify the file name of ouput of report_global_timing -format csv" AString string optional}
    {-outputDirOfViolList "specify the output directory of viol list to dump when parsing report_constraint rpt files" AString string optional}
    {-prefixOutPutFile "specify the prefix generating viol list file when parsing report_constraint rpt files" AString string optional}
    {-waive_list_file "specify the file of waive list that is support regexp, which need to write prefix of regexp like: 'line 1: TRAN regexp:{^U_ANARF_TOP/\S+$}'" AString string optional}
    {-waive_prefix_prompt_list "specify the prefix prompts list of waive flag, it is D2 list, like {{max_delay SETUP} {min_delay HOLD} ...}" AList list optional}
  }
